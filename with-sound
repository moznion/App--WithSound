#!/usr/bin/env perl
use strict;
use warnings;
use Audio::Play::MPG123;
use Config::Simple;
use File::Spec::Functions qw/catfile/;

sub load_config {
    my $rc_file_name = '.with-soundrc';
    my $rc_file_location = catfile( $ENV{HOME}, $rc_file_name );

    # Not exists rc file on home directory.
    unless ( -f $rc_file_location ) {
        die "[ERROR] Please put '$rc_file_name' file on your home directory\n";
    }

    my $config = Config::Simple->new($rc_file_location);
    return $config->param('SUCCESS'), $config->param('FAILURE');
}

sub play_mp3 {
    my ( $mp3_file, $status ) = @_;

    if ( -f $mp3_file ) {
        my $pid = fork;
        die "fork failed." unless defined $pid;

        if ( $pid == 0 ) {
            my $player = Audio::Play::MPG123->new;
            $player->load($mp3_file);
            $player->poll(1) until $player->state == 0;
            exit;
        }
    }
    else {
        warn "[WARNING] Sound file is not specified for when $status. "
          . "Please check your .with-soundrc file.\n";
    }
}

unless (@ARGV) {
    die 'Usage: $ with-sound [command] ([argument(s)])' . "\n";
}

my ( $success_sound, $failure_sound ) = load_config;
$success_sound = glob $success_sound;
$failure_sound = glob $failure_sound;

my $retval = system(@ARGV);

if ( $retval == 0 ) {

    # success
    play_mp3( $success_sound, 'success' );
}
else {
    # failure
    play_mp3( $failure_sound, 'failure' );
}

$retval = 1 if $retval > 255;
exit($retval);

__END__

=coding utf8

=head1 NAME

with-sound - Command executor with sound


=head1 VERSION

This document describes with-sound version 1.0.0


=head1 SYNOPSIS

    $ with-sound [command] ([argument(s)])


=head1 DESCRIPTION

with-sound is the command executor with sound (now with-sound can play the MP3 file only).

If exit-status of the command is success, it playbacks the sound of success.
Else, it plays sound of failure.


=head1 CONFIGURATION AND ENVIRONMENT

App::WithSound requires a resource file as '.with-soundrc' on your home directory.
This resource file specifies the path to sound files. Like so:

    SUCCESS: ~/Music/success.mp3
    FAILURE: ~/Music/failure.mp3

And this module requires mpg123 so please install it before use this application.

L<<http://www.mpg123.de/index.shtml>>


=head1 DEPENDENCIES

Audio::Play::MPG123 (version 0.63 or later)

Config::Simple (version 0.48 or later)

File::Spec::Functions (version 3.40 or later)


=head1 BUGS AND LIMITATIONS

No bugs have been reported.

Please report any bugs or feature requests to
C<bug-app-withsound@rt.cpan.org>, or through the web interface at
L<http://rt.cpan.org>.


=head1 AUTHOR

moznion  C<< <moznion@gmail.com> >>

Shinpei Maruyama C<< <shinpeim[at]gmail.com> >>


=head1 LICENCE AND COPYRIGHT

Copyright (c) 2013, moznion C<< <moznion@gmail.com> >>. All rights reserved.

This module is free software; you can redistribute it and/or
modify it under the same terms as Perl itself. See L<perlartistic>.


=head1 DISCLAIMER OF WARRANTY

BECAUSE THIS SOFTWARE IS LICENSED FREE OF CHARGE, THERE IS NO WARRANTY
FOR THE SOFTWARE, TO THE EXTENT PERMITTED BY APPLICABLE LAW. EXCEPT WHEN
OTHERWISE STATED IN WRITING THE COPYRIGHT HOLDERS AND/OR OTHER PARTIES
PROVIDE THE SOFTWARE "AS IS" WITHOUT WARRANTY OF ANY KIND, EITHER
EXPRESSED OR IMPLIED, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE
ENTIRE RISK AS TO THE QUALITY AND PERFORMANCE OF THE SOFTWARE IS WITH
YOU. SHOULD THE SOFTWARE PROVE DEFECTIVE, YOU ASSUME THE COST OF ALL
NECESSARY SERVICING, REPAIR, OR CORRECTION.

IN NO EVENT UNLESS REQUIRED BY APPLICABLE LAW OR AGREED TO IN WRITING
WILL ANY COPYRIGHT HOLDER, OR ANY OTHER PARTY WHO MAY MODIFY AND/OR
REDISTRIBUTE THE SOFTWARE AS PERMITTED BY THE ABOVE LICENCE, BE
LIABLE TO YOU FOR DAMAGES, INCLUDING ANY GENERAL, SPECIAL, INCIDENTAL,
OR CONSEQUENTIAL DAMAGES ARISING OUT OF THE USE OR INABILITY TO USE
THE SOFTWARE (INCLUDING BUT NOT LIMITED TO LOSS OF DATA OR DATA BEING
RENDERED INACCURATE OR LOSSES SUSTAINED BY YOU OR THIRD PARTIES OR A
FAILURE OF THE SOFTWARE TO OPERATE WITH ANY OTHER SOFTWARE), EVEN IF
SUCH HOLDER OR OTHER PARTY HAS BEEN ADVISED OF THE POSSIBILITY OF
SUCH DAMAGES.
